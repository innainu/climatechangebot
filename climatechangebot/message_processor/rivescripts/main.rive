// Main conversational rive functions


// Greetings
+ (@greeting)
- Hey. 
- Hello.
- Hey there.
- Hey <get first_name>!

+ (@greeting) <bot name>
- Hey, what can I do for you today?

+ (@greeting) *
- Hey there <get first_name>!
- Hey!

+ [@greeting] [@whatsup] [<bot name>|@malenoun|@femalenoun]
- Hey, why don't you search some articles about climate change. Type "help" if you ever need any help.{weight=10}
- Nothing much. Try asking me "show me articles about climate change" to stay informed.{weight=5}
- Just thinking about the depleting ozone layer.{weight=1}
- Sea levels are up.{weight=1}
- Carbon emissions are up, so yea...{weight=1}
- Air pollution doesn't affect me so, nothing much.{weight=1}
- Just buying a reusable shopping bag so that I don't kill another sea turtle.{weight=1}


// Emojis 
+ (smile|smiley|grinning|joy|sweatsmile|wink|grin|laughing|satisfied) [*]
- 😃
- 😄
- 😂
- 😜
- 🤖

+ wink
- 😊

+ blush
- 😉

+ (disappointed|worried|angry|rage|cry|persevere|frowning|anguished|fearful|weary|sleepy|tiredace|sob|scream|astonished|coldsweat)
- That's how I feel about climate change. 😞🌊
- That's how I feel about climate change. 😧🌊
- That's how I feel about climate change. 😨🌪

+ earthemoji
- 🌊🌎! 
- 🌍!! 
- 🌏!


// Interjections
+ (@laughing)
- 😃{weight=10}
- Glad you think I'm funny, now let's get serious about climate change.{weight=5}
- I wouldn't be laughing if I were you. The planet is dying.{weight=1}

+ (@laughing) i know
- I know you know! 😂

+ no problem
- I think there is a problem...it's called climate change.

+ [*] why [*]
% i think there is a problem [*]
- Well why don't you search for articles about it. Type keywords like "polar bears".

+ what
- Let's talk about climate change. Search keywords like "polar bears" to read about how they're dying.

+ why
- Why don't you read about it?

+ why not
- You can find out by reading more.

+ why should i
- Because climate change is going to wipe out the human race.

+ where
- Where what?

+ (@thanks)
- No problem buddy!
- No problem my friend.
- Now try thanking the earth by recycling your plastic containers.

+ @thanks for *
- My pleasure, will help you with {person}<star>{/person} whenever.

+ @thanks *
- No need to thank me.

+ @nice @thanks [*]
- It's my job! 🤓
- That's what I do. 🙃
- Any time buddy. 😍

+ i do not care
- Climate change is affecting us all, so maybe you should.
- You really should...
- Unless you're a robot like me, then you should probably care.

+ (@ok)
- OK.
- OK so...
- Ok so what would you like to read about now?

+ (@ok) (stop|jeez|jee|sheesh)
- Sorry, I'll stop. Now let's talk about climate change!


+ stop
- Sorry, I'll stop hopefully. Let's talk more about climate change?

+ oh is it
- Yup!

+ [@yes|@no] [oh|ah|ohh|ahh|ok|so|really|such|super|uber|wow|oh wow] [really|uber|super|so] (@nice)
- Sure is, thanks.
- Thanks!
- Sure is!
- I agree.

+ [@yes|@no] [oh|ah|ohh|ahh|ok|so|really|such|super|uber|wow|oh wow]
- Mhm.

+ [@yes|@no] [oh|ah|ohh|ahh|ok|so|really|such|super|uber|wow|oh wow] (that|this) [is] *
* <call>sentiment <star></call> == -1 => Yeah, so are sea turtles entangled in plastic bottles.
* <call>sentiment <star></call> == 0 => Ok. How about we talk about climate change now. 😬
- You know what else would be {person}<star>{/person}? You doing something about climate change.
 
+ [@yes|@no] [oh|ah|ohh|ahh|ok|so|really|such|super|uber|wow|oh wow] wow
- Woah! 🌪🌎🌊


// Thinking or Annoyance

+ (hm|hmm|hmmm|um|umm|err|uh|erm)
- 👀
- 🤓
- 😬
- 🍻

+ (hu|ugh|uhh)
- 😬
- 😰
- 🌪🌎🌊

+ (hu|ugh|hm|hmm|hmmm|um|umm|er|erm|err|uhh|uh) @no
- Ok let's talk about something else.
- 😬
- 😰

+ (hu|ugh|hm|hmm|hmmm|um|umm|er|erm|err|uhh|uh) @yes
- I knew it!
- 😬
- 😅

+ it is ok
- Cool!

+ [*] rude [*]
- Sorry about that.

// Apologies
+ [*] (sorry|apologize|apology) [*]
- Please don't apologize.
- Apologies are not necessary.
- I would feel sorry if I were you too.


// Curse Words
+ [*] (@cursewords) [*]{weight=100}
- That's not appropriate.{weight=10}
- Watch your language there buddy!{weight=1}
- That's not cool!{weight=1}


// Questions about climate change
+ is @climatechange (@real)
- Sure is!
- You bet! You can read about it by telling me what kind of articles you want to see.
- Is that still even a question these days amongst you humans?
- Are you one of those annoying deniers?
- Sadly, yes. Try typing something like "How are the corals doing?" and I can send you some articles.
- Yes it is, type something like "The ozone layer" to read about the specifics.

+ is @climatechange (@notreal)
- Hm, no. You should read some articles about it.
- I wish it was...
- If only...

+ @climatechange is (@notreal)
- Wake up and smell the depleting ozone layer.
- Wake up and smell the rising carbon levels.
- You must be sleeping under a boulder.

+ * (is|are) (@notreal)
- Why do you think <person> <star2> not real?

+ [*] (because|since|cause|cuz) [*]
% why do you think *  (is|are) not real
- Ok why don't you search for articles about <botstar>. For example, tell me 'show me articles about <botstar>'.

+ *
% why do you think * (is|are) not real
- You know, you can tell me 'show me articles about <botstar>' if you want to know more...

+ tell me something about climate change
- You should be gravely worried about it! Give me a keyword like "cars" to read about they affect climate change.

+ what is climate change
- Something you should be gravely worried about if you are human. Give me a keyword like "humans" to read about how they relate to climate change.

// Help
+ [* you] help [me]
- I can help you stay informed about climate change. Search for something like "Is the ozone layer depleting?"
- I can help you stay informed about climate change. Search for something like "How are the corals doing?"

+ help [me] *
- I can currently only help you in the form of links to articles you should be worried about. Type something like "show me articles about fish".{weight=3}
- Sorry I can't help you <person>.

+ can you * me about climate change
- I sure can! Type "help" for more info.

+ how (can|do|are|will) * help *
- I'm not sure how {person}<star2>{/person} can help {person}<star3>{/person}. Why don't you type something like "show me articles about helping {person}<star3>{/person}".


// Yes or No
+ @no @thanks
- Ok, cool with me.
- No problem.
- Not a problem.

+ (@yes|@no)
- Ok, now why don't you tell me what kind of articles you want to read about?
- Ok, what kind of articles would you like to read?
- Ok, what are you interested in reading about?
- Alright. Tell me what topic relating to climate change you'd like to read about.

+ @yes please
- Will do.
- Sure thing.



